plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

ext {
    sdkVersion = "0.3.6-rc.2"
}

def getVersionName = { String flavorName ->
    exec {
        commandLine 'git', 'fetch', '--all', '--tags'
    }

    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--abbrev=0', '--always'
        standardOutput = stdout
    }

    def baseVersionName = stdout.toString().trim()

    def commitHashStdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = commitHashStdout
    }
    def commitHash = commitHashStdout.toString().trim()

    return flavorName == "production" ? "$baseVersionName-$commitHash-sdk$sdkVersion" : "$baseVersionName-$commitHash-sdk$sdkVersion-$flavorName"
}

android {
    namespace 'media.opensesame.syncstagetestappandroid'
    compileSdk 33

    defaultConfig {
        applicationId "media.opensesame.syncstagetestappandroid"
        minSdk 29
        targetSdk 33
        versionCode 1

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        defaultPublishConfig "production"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.1.1'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    flavorDimensions "environment"

    productFlavors {
        development {
            dimension "environment"
            versionName getVersionName("development")
            buildConfigField "String", "VERSION_NAME", "\"${getVersionName("development")}\""
            defaultConfig {
                archivesBaseName = "$applicationId-${getVersionName('development')}"
            }
        }
//        staging {
//            dimension "environment"
//            versionName getVersionName("staging")
//            buildConfigField "String", "VERSION_NAME", "\"${getVersionName("staging")}\""
//            defaultConfig {
//                archivesBaseName = "$applicationId-${getVersionName('staging')}"
//            }
//        }
//        production {
//            dimension "environment"
//            versionName getVersionName("production")
//            buildConfigField "String", "VERSION_NAME", "\"${getVersionName("production")}\""
//            defaultConfig {
//                archivesBaseName = "$applicationId-${getVersionName('production')}"
//            }
//        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}


dependencies {
//    productionImplementation "media.opensesame:syncstagesdk:${sdkVersion}"
    developmentImplementation "media.opensesame:syncstagesdk:${sdkVersion}-development"
//    stagingImplementation "media.opensesame:syncstagesdk:${sdkVersion}-staging"

    implementation platform('com.google.firebase:firebase-bom:31.2.2')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.4.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material-icons-extended:1.3.1"
    implementation "androidx.compose.material3:material3:1.0.1"
    implementation 'androidx.compose.material:material:1.3.1'
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation 'androidx.navigation:navigation-compose:2.5.3'
    implementation 'com.google.accompanist:accompanist-permissions:0.28.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}