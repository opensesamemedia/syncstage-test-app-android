plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'

     // TO BE UNCOMMENTED FOR CRASHLYTICS
     id 'com.google.gms.google-services'
     id 'com.google.firebase.crashlytics'
}



def getVersionName = { String flavorName ->
    def appVersion = "$System.env.APP_VERSION"
    def commitSha = "$System.env.GITHUB_SHA"

    if (appVersion != null && !appVersion.isEmpty()) {
        return flavorName == "master" ? appVersion : "$appVersion-$flavorName"

    } else {
        // Do something when APP_VERSION is empty or not set
        println "APP_VERSION is empty or not set"
        return flavorName == "master" ? commitSha : "$commitSha-$flavorName"
    }
}

android {
    namespace 'media.opensesame.syncstagetestappandroid'
    compileSdk 33

    defaultConfig {
        applicationId "media.opensesame.syncstagetestappandroid"
        minSdk 29
        targetSdk 33
        versionCode 1

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.1.1'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    flavorDimensions "environment"

    productFlavors {
        development {
            dimension "environment"
            versionName getVersionName("development")
            buildConfigField "String", "VERSION_NAME", "\"${getVersionName("development")}\""
            buildConfigField "String", "API_URL", "\"https://f55pesypsd.execute-api.us-east-1.amazonaws.com/dev/\""
            defaultConfig {
                archivesBaseName = "$applicationId-${getVersionName('development')}"
            }
        }
        staging {
            dimension "environment"
            versionName getVersionName("staging")
            buildConfigField "String", "VERSION_NAME", "\"${getVersionName("staging")}\""
            buildConfigField "String", "API_URL", "\"https://3bty5ooqg7.execute-api.us-east-1.amazonaws.com/staging/\""
            defaultConfig {
                archivesBaseName = "$applicationId-${getVersionName('staging')}"
            }
        }
        master {
            dimension "environment"
            versionName getVersionName("master")
            buildConfigField "String", "API_URL", "\"https://api.sync-stage.com/\""
            buildConfigField "String", "VERSION_NAME", "\"${getVersionName("master")}\""
            defaultConfig {
                archivesBaseName = "$applicationId-${getVersionName('master')}"
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

// Used when built from package, not sources
ext {
    sdkVersion = "0.5.2"
}

dependencies {
    masterImplementation "media.opensesame:syncstagesdk:${sdkVersion}"
    developmentImplementation "media.opensesame:syncstagesdk:${sdkVersion}-development"
    stagingImplementation "media.opensesame:syncstagesdk:${sdkVersion}-staging"


    // TO BE UNCOMMENTED FOR CRASHLYTICS
    implementation platform('com.google.firebase:firebase-bom:31.2.2')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'


    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-service:2.4.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material-icons-extended:1.3.1"
    implementation "androidx.compose.material3:material3:1.0.1"
    implementation 'androidx.compose.material:material:1.3.1'
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation 'androidx.navigation:navigation-compose:2.5.3'
    implementation 'com.google.accompanist:accompanist-permissions:0.28.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    // HTTP requests for provisioning dependencies:
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.0"

    // CameraX
    implementation "androidx.camera:camera-camera2:1.0.2"
    implementation "androidx.camera:camera-lifecycle:1.0.2"
    implementation "androidx.camera:camera-view:1.0.0-alpha31"

    // Zxing
    implementation 'com.google.zxing:core:3.3.3'

}

// Allow references to generated code
kapt {
    correctErrorTypes true
}
